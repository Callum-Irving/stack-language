WHITESPACE = _{ "\n" | "\r" | "\t" | " " }
COMMENT = _{ "#" ~ (!"\n" ~ ANY)* ~ "\n" }

program = _{ SOI ~ decl+ ~ EOI }

decl = _{ constant | function }

constant = { "const" ~ ident ~ "=" ~ ( integer ~ "bytes" | literal ) }

function = { "let" ~ ident ~ "(" ~ inputs ~ outputs ~ ")" ~ "=" ~ "{" ~ expr ~ "}" }
inputs = { basictype* }
outputs = { ("->" ~ basictype+)? }

expr = { stmt+ }

stmt = {
	literal
    | ifstmt
    | mathop
    | comparisonop
    | ident
}

ifstmt = { "IF" ~ "{" ~ expr ~ "}" ~ ("ELSE" ~ "{" ~ expr ~ "}")? }
mathop = { "+" | "-" | "*" | "/" | "%" }
comparisonop = { ">" | "<" | "=?" | "!=" }

basictype = { "ptr" | "int" }
ident = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
literal = { integer | string }
integer = @{ ASCII_DIGIT+ }
string = @{ "\"" ~ ( (!("\"" | "\\") ~ ANY) | ("\\" ~ predefined) )* ~ "\"" }
predefined = _{ "n" | "r" | "t" | "\\" | "0" | "\"" | "'" }
